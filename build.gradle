plugins {
    //see https://imperceptiblethoughts.com/shadow/getting-started/#default-java-groovy-tasks
    id 'com.github.johnrengelman.shadow' version '4.0.0'
    id 'groovy'
    id 'java'
}

group 'com.softwood'
version '1.0-SNAPSHOT'

defaultTasks = ['shadowJar']


sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.6'
    compile fileTree(dir: 'lib', include: ['*.jar'])  //use to include the weblogic jar 
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

//project.tasks.remove jar

shadowJar {
    manifest {
        //set the main class same as name of the script you want to run
        attributes 'Main-Class': 'com.softwood.cli.Launcher'
    }

    classifier = ""
}


//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'com.softwood.cli.Launcher'
    }
    baseName = 'client' + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}